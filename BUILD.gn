#Copyright (c) 2024 Huawei Device Co., Ltd.
#Licensed under the Apache License, Version 2.0 (the "License");
#you may not use this file except in compliance with the License.
#You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
#Unless required by applicable law or agreed to in writing, software
#distributed under the License is distributed on an "AS IS" BASIS,
#WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#See the License for the specific language governing permissions and
#limitations under the License.

import("//build/ohos.gni")

SANE_CONFIG_DIR = "/data/service/el2/public/print_service/sane/config"
SANE_DATA_DIR = "/data/service/el2/public/print_service/sane/data"
SANE_LOCK_DIR = "/data/service/el2/public/print_service/sane/lock"
SANE_V_MAJOR = 1
SANE_V_MINOR = 2
SANE_LIB_DIR = "/data/service/el2/public/print_service/sane/backend"
enable_hilog = true
source_dir = "//third_party/backends"
target_dir = "${target_gen_dir}/sane"

backends_usb_manager_source_file = [
  "$target_dir/src/sanei_usb.c",
  "$target_dir/src/usb_manager.cpp"
]

backends_usb_header_file = [
  "$target_dir/include/config.h",
  "$target_dir/include/usb_manager.h",
]

action("backends_action") {
  print("backends_action is exec")
  script = "//third_party/backends/install.py"
  outputs = []
  outputs += backends_usb_manager_source_file
  outputs += backends_usb_header_file

  inputs = [ "${source_dir}/patches/usbmanager.patch" ]
  backends_source_dir = rebase_path("${source_dir}", root_build_dir)
  backends_target_dir = rebase_path("${target_dir}", root_build_dir)
  args = [
    "--gen-dir",
    "$backends_target_dir",
    "--source-dir",
    "$backends_source_dir",
  ]
}

base_deps = [
  "//third_party/libxml2:xml2",
  "//third_party/libpng:libpng",
  "//third_party/libjpeg-turbo:turbojpeg",
]

config("base_config") {
  include_dirs = [
    ".",
    "./include",
    "./include/sane",
    "//third_party/libxml2/libxml2-2.9.14/include",
    "//third_party/libjpeg-turbo",
    "//third_party/libpng/libpng-1.6.37"
  ]
    
  cflags = [
    "-Wall",
    "-g",
    "-O2",
    "-fPIC",
    "-DPIC",
    "-D_REENTRANT",
    "-DHAVE_CONFIG_H",
    "-Wno-format",
    "-Wno-unused-const-variable",
    "-Wno-unused-variable",
    "-Wno-unused-but-set-variable",
  ]

  if (enable_hilog) {
    cflags += [ "-DENABLE_HILOG" ]
  }

  defines = [
    "PATH_SANE_CONFIG_DIR=$SANE_CONFIG_DIR",
    "PATH_SANE_DATA_DIR=$SANE_DATA_DIR",
    "PATH_SANE_LOCK_DIR=$SANE_LOCK_DIR",
    "V_MAJOR=$SANE_V_MAJOR",
    "V_MINOR=$SANE_V_MINOR",
  ]
}

config("backend_config") {
  configs = [":base_config"]

  defines = ["LIBDIR=\"$SANE_LIB_DIR\""]
}

#build targets in /lib
ohos_source_set("lib") {
  sources = ["./lib/md5.c"]

  configs = [":base_config"]

  deps = base_deps

  subsystem_name = "thirdparty"
  part_name = "backends"
}

#build targets in /sanei
sanei_names = [
  "sanei_directio",
  "sanei_ab306",
  "sanei_constrain_value",
  "sanei_init_debug",
  "sanei_net",
  "sanei_wire",
  "sanei_codec_ascii",
  "sanei_codec_bin",
  "sanei_scsi",
  "sanei_config",
  "sanei_config2",
  "sanei_pio",
  "sanei_pa4s2",
  "sanei_auth",
  "sanei_thread",
  "sanei_pv8630",
  "sanei_pp",
  "sanei_lm983x",
  "sanei_access",
  "sanei_tcp",
  "sanei_udp",
  "sanei_magic",
  "sanei_ir",
  "sanei_jpeg",
]

foreach (name, sanei_names) {
  ohos_source_set("$name") {
    sources = ["./sanei/$name.c"]

    deps = base_deps

    if (enable_hilog) {
        external_deps = [
        "hilog:libhilog",
        ]
    }

    configs = [":base_config"]

    subsystem_name = "thirdparty"
    part_name = "backends"
  }
}

ohos_source_set("sanei_usb") {
  include_dirs = [
    "$target_dir/include"
  ]
  sources = [
    "$target_dir/src/sanei_usb.c",
    "$target_dir/src/usb_manager.cpp"
  ]

  deps = base_deps
  deps += [
    ":backends_action"
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "usb_manager:usbsrv_client",
    "bounds_checking_function:libsec_shared"
  ]

  configs = [":base_config"]

  subsystem_name = "thirdparty"
  part_name = "backends"
}

ohos_static_library("sanei") {
  sources = []

  foreach(name, sanei_names) {
    sources += ["./sanei/$name.c"]
  }

  if (enable_hilog) {
    external_deps = [
    "hilog:libhilog",
    ]
  }
  
  configs = [":base_config"]

  subsystem_name = "thirdparty"
  part_name = "backends"
}

#build targets in /backend
ohos_source_set("sane_strstatus") {
  sources = ["./backend/sane_strstatus.c"]
    
  configs = [":backend_config"]

  subsystem_name = "thirdparty"
  part_name = "backends"
}

ohos_shared_library("sane") {
  sources = [
    "./backend/stubs.c",
    "./backend/dll.c",
  ]
  
  configs = [":backend_config"]
    
  defines = ["BACKEND_NAME=dll"]

  deps = [
    ":sanei_init_debug",
    ":sanei_constrain_value",
    ":sanei_config",
    ":sane_strstatus",
    ":sanei_usb",
    ":lib",
  ]

  deps += base_deps

  if (enable_hilog) {
    external_deps = [
    "hilog:libhilog",
    ]
  }
  
  ldflags = ["-ldl"]

  subsystem_name = "thirdparty"
  part_name = "backends"
}

#the target group
group("third_sane") {
  deps = [
    ":sane"
  ]
}